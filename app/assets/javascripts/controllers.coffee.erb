class @PhotoGalleryCtrl extends Router
  routes:->
    {
      default: '/photographers'
      '/photographers':
        template: '<%= asset_path("photographers.html") %>'
        controller: PhotographersCtrl
      '/photographers/:photographer_id/galleries':
        template: '<%= asset_path("galleries.html") %>'
        controller: GalleriesCtrl
      '/photographers/:photographer_id/galleries/:gallery_id/photos':
        template: '<%= asset_path("photos.html") %>'
        controller: PhotosCtrl
    }

PhotoGalleryCtrl.$inject = ['$route', '$xhr']

class @PhotographersCtrl
  constructor:(@Photographers)->
    @photographers = @Photographers.index()

PhotographersCtrl.$inject = ['Photographers']


class @GalleriesCtrl
  constructor: (Galleries, Photographers, $routeParams)->
    @photographer = Photographers.get photographer_id: $routeParams.photographer_id
    @galleries = Galleries.index photographer_id: $routeParams.photographer_id

GalleriesCtrl.$inject =  ['Galleries', 'Photographers', '$routeParams']



class @PhotosCtrl
  constructor:(Photos, Galleries, Photographers, @SelectedPhotos, $routeParams)->

    @photographer = Photographers.get
      photographer_id: $routeParams.photographer_id
    @gallery = Galleries.get
      photographer_id: $routeParams.photographer_id
      gallery_id: $routeParams.gallery_id
    @photos = Photos.index
      photographer_id: $routeParams.photographer_id
      gallery_id: $routeParams.gallery_id

    @selected_photos = SelectedPhotos.index()


  selectPhoto: (photo)->
    selected_photo = new @SelectedPhotos selected_photo: { photo_id: photo.id }
    selected_photo.$create ()=>
      @selected_photos.push(selected_photo)

  deleteSelectedPhoto:(selected_photo)->
    angular.Array.remove @selected_photos, selected_photo
    selected_photo.$destroy selected_photo_id: selected_photo.id

  saveSelectedPhoto :(selected_photo)->
    selected_photo.$update selected_photo_id: selected_photo.id
    $('input').blur()

PhotosCtrl.$inject = ['Photos', 'Galleries', 'Photographers', 'SelectedPhotos', '$routeParams']
